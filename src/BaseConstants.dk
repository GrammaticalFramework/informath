Set : Type.
Prop : Type.

(; ignored in Dedukti2Core ;) 
Elem : Set -> Type.
Proof : Prop -> Type.

(; logical operators, hard-coded in MathCore ;)
false : Prop.
and : (A : Prop) -> (B : Prop) -> Prop.
or : (A : Prop) -> (B : Prop) -> Prop.
if : Prop -> Prop -> Prop.
forall : (A : Set) -> (Elem A -> Prop) -> Prop.
exists : (A : Set) -> (Elem A -> Prop) -> Prop.

def not : Prop -> Prop := A => if A false.
def iff : Prop -> Prop -> Prop := A => B => and (if A B) (if B A).

(; number domains, meant to be increasing subsets of Complex ;)
(; but the simplest thing is to have just one set Number; cf. Ganesalingam p. 126-  ;)
Num : Set.
def Dig : Set := Num.
def Nat : Set := Num.
def Int : Set := Num.
def Rat : Set := Num.
def Real : Set := Num.
def Complex : Set := Num.
def Cardinal : Set := Num.

(; digits ;)
0 : Elem Dig.
1 : Elem Dig.
2 : Elem Dig.
3 : Elem Dig.
4 : Elem Dig.
5 : Elem Dig.
6 : Elem Dig.
7 : Elem Dig.
8 : Elem Dig.
9 : Elem Dig.

(; slightly non-canonical representation of natural numbers in base 10 ;)
(; 123 = nn 1 (nn 2 (nd 3)) ;)
(; leading zeros should be ignored ;)
nd : Elem Dig -> Elem Nat.
nn : Elem Dig -> Elem Nat -> Elem Nat.

(; basic comparison relations ;)
Eq : (x : Elem Num) -> (y : Elem Num) -> Prop.
Lt : Elem Num -> Elem Num -> Prop.
Gt : Elem Num -> Elem Num -> Prop.
Neq : Elem Num -> Elem Num -> Prop.
Leq : Elem Num -> Elem Num -> Prop.
Geq : Elem Num -> Elem Num -> Prop.

(; type-relative equality often used ;)
Eqt : (A : Set) -> Elem A -> Elem A -> Prop.

positive : (x : Elem Real) -> Prop.
negative : Elem Real -> Prop.

(; basic arithmetic operations ;)
plus : (x : Elem Num) -> (y : Elem Num) -> Elem Num.
minus : Elem Num -> Elem Num -> Elem Num.
times : Elem Num -> Elem Num -> Elem Num.
div : Elem Num -> Elem Num -> Elem Num.
pow : Elem Num -> Elem Num -> Elem Num.
neg : Elem Num -> Elem Num.
sqrt : Elem Num -> Elem Num.
log : Elem Num -> Elem Num -> Elem Num.

succ : Elem Nat -> Elem Nat.

gcd : Elem Int -> Elem Int -> Elem Int.

abs : Elem Real -> Elem Real.
factorial : Elem Nat -> Elem Nat.

(; predicates of integers ;)
even : Elem Int -> Prop.
def odd : Elem Int -> Prop := n => not (even n).

def divisible : Elem Int -> Elem Int -> Prop := n => m => exists Int (k => Eq n (times k m)).
def divide : Elem Int -> Elem Int -> Prop := n => m => divisible m n.
def divisor : Elem Int -> Elem Int -> Prop := divide.

def prime : Elem Nat -> Prop := n => not (exists Nat (m => and (Lt 1 m) (and (Lt m n) (divisible n  m)))).

def bool : Set.

def List : Set -> Set.
member : A : Set -> Elem A -> Elem (List A) -> Prop.

(; set operations ;)
function : Set -> Set -> Set.
union : Set -> Set -> Set.
intersection : Set -> Set -> Set.
difference : Set -> Set -> Set.
cartesian : Set -> Set -> Set.
complement : Set -> Set.
powerset : Set -> Set.

emptyset : Set.
universeset : Set.

subset : Set -> Set -> Prop.
subseteq : Set -> Set -> Prop.
superset : Set -> Set -> Prop.
superseteq : Set -> Set -> Prop.
equalset : Set -> Set -> Prop.
element : Elem universeset -> Set -> Prop.
notelement : Elem universeset -> Set -> Prop.

positivePart : Set -> Set.
negativePart : Set -> Set.

finite : Set -> Prop.
def infinite : Set -> Prop := A => not (finite A).

suchthat : (A : Set) -> (B : Elem A -> Prop) -> Set.
fst : (A : Set) -> (B : (Elem A -> Prop)) -> Elem (suchthat A B) -> Elem A.

def congruent : (a : Elem Int) -> (b : Elem Int) -> (m : Elem Nat) -> Prop :=
  a => b => m => exists Nat (k => Eq (minus a b) (times k m)).

Enum : Set.
nil : Elem Enum.
cons : Elem Num -> Elem Enum -> Elem Enum.

def enum123 : Elem Enum := cons 1 (cons 2 (cons 3 nil)).

enumset : Elem Enum -> Set.
