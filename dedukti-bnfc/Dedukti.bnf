entrypoints Module ;

MJmts. Module ::= [Jmt] ;

terminator Jmt "" ;

comment "(;" ";)" ;
comment "#" ; ----

JTyping.  Jmt ::= QIdent ":" Exp "." ;
JDef.     Jmt ::= "def" QIdent MTyp MExp "." ;
JThm.     Jmt ::= "thm" QIdent MTyp MExp "." ;
JRewr.    Jmt ::= [Rewr] "." ;

RRule.  Rewr ::= "[" [QIdent] "]" Patt "-->" Exp ;
separator nonempty Rewr "" ;

separator QIdent "," ;

MTNone. MTyp ::= ;
MTExp.  MTyp ::= ":" Exp ;

MENone. MExp ::= ;
MEExp.  MExp ::= ":=" Exp ;

EIdent.  Exp9 ::= QIdent ;
---- EQual.   Exp8 ::= Exp8 "." QIdent ; 
EApp.    Exp5 ::= Exp5 Exp6 ;
EAbs.    Exp2 ::= Bind "=>" Exp2 ;
EFun.    Exp1 ::= Hypo "->" Exp1 ;

coercions Exp 9 ;

BVar.   Bind ::= Var ;
VTyped. Bind ::= Var ":" Exp3 ;

VIdent.  Var ::= QIdent ;
VWild.   Var ::= "_" ;

HExp.    Hypo ::= Exp3 ;
HVarExp. Hypo ::= Var ":" Exp3 ;

PVar.   Patt3 ::= Var ;
PApp.   Patt1 ::= Patt1 Patt2 ;
PBind.  Patt  ::= Bind "=>" Patt1 ;

coercions Patt 3 ;

token QIdent (letter (letter | digit | '_')* ('.' (letter | digit | '_')+)*) ;
